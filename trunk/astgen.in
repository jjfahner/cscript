//////////////////////////////////////////////////////////////////////////
//
// This file is © 2007 JJ Fahner <jan-jaap@jan-jaap.net>
// This file is part of the cscript interpreter.
// CScript can be found at http://svn.jan-jaap.net/
//
// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.
// 
// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.
// 
// You should have received a copy of the GNU General Public License
// along with this program.  If not, see <http://www.gnu.org/licenses/>.
//
//////////////////////////////////////////////////////////////////////////

//
// Lemon language definition for cscript
//

//////////////////////////////////////////////////////////////////////////
//
// Include section
//
%include {

#include "tokens.h"
#include "ast.h"

#pragma warning(disable:4065)

} 
//
// End inclusion block
//
//////////////////////////////////////////////////////////////////////////

// Set prefix for tokens
%name             AstGenParse
%token_prefix     TOK_
%token_type       { Token }
%extra_argument   { AstGen* p }
%default_type     { Ast* }

// Error handlers
%parse_failure {
  p->OnParseFailure();
}
%syntax_error {
  p->OnSyntaxError();
}

// Precedence rules for binary operators
%left LOGOR .
%left LOGAND .
%left BITOR .
%left BITXOR .
%left BITAND .
%left EQUALS NEQUALS .
%left ST SE GT GE .
%left ADDOP SUBOP .
%left MULOP DIVOP MODOP .
%right ASSIGN ASSADD ASSSUB ASSMUL ASSDIV ASSMOD .

// Start symbol
translation_unit ::= statement_sequence_opt(A) . { p->SetRoot(A); }

// Statement sequence
statement_sequence(R) ::= statement(A) . { R = A; }
statement_sequence(R) ::= statement_sequence(A) statement(B) . { R = new Ast(statement_sequence, A, B); }

// Optional statement sequence
statement_sequence_opt(R) ::= . { R = 0; }
statement_sequence_opt(R) ::= statement_sequence(A) . { R = A; }

// Statement
statement(R) ::= include_statement(A) .     { R = A; }
statement(R) ::= expression_statement(A) .  { R = A; }
statement(R) ::= declaration_statement(A) . { R = A; }
statement(R) ::= for_statement(A) .         { R = A; }
statement(R) ::= compound_statement(A) .    { R = A; }
statement(R) ::= if_statement(A) .          { R = A; }
statement(R) ::= while_statement(A) .       { R = A; }
statement(R) ::= foreach_statement(A) .     { R = A; }
statement(R) ::= return_statement(A) .      { R = A; }

//////////////////////////////////////////////////////////////////////////
//
// Expressions
//

// Expression
expression(R) ::= assignment_expression(A) . { R = A; }

// Optional expression
expression_opt(R) ::= .               { R = 0; }
expression_opt(R) ::= expression(A) . { R = A; }

// Assignment
assignment_expression(R) ::= conditional_expression(A) . { R = A; }
assignment_expression(R) ::= binary_expression(A) assignment_operator(T) assignment_expression(B) . { R = new Ast(assignment_expression, T, A, B); }

// Assignment operator
%type assignment_operator { int }
assignment_operator(R) ::= ASSIGN . { R = TOK_ASSIGN; }
assignment_operator(R) ::= ASSADD . { R = TOK_ASSADD; }
assignment_operator(R) ::= ASSSUB . { R = TOK_ASSSUB; }
assignment_operator(R) ::= ASSMUL . { R = TOK_ASSMUL; }
assignment_operator(R) ::= ASSDIV . { R = TOK_ASSDIV; }
assignment_operator(R) ::= ASSMOD . { R = TOK_ASSMOD; }

// Conditional
conditional_expression(R) ::= binary_expression(A) . { R = A; }
conditional_expression(R) ::= binary_expression(A) QUESTION expression(B) COLON assignment_expression(C) . { R = new Ast(ternary_expression, A, B, C); }

// Binary expressions
binary_expression(R) ::= unary_expression(A) . { R = A; }
binary_expression(R) ::= binary_expression(A) LOGOR   binary_expression(B). { R = new Ast(binary_expression, TOK_LOGOR,   A, B); }
binary_expression(R) ::= binary_expression(A) LOGAND  binary_expression(B). { R = new Ast(binary_expression, TOK_LOGAND,  A, B); }
binary_expression(R) ::= binary_expression(A) BITOR   binary_expression(B). { R = new Ast(binary_expression, TOK_BITOR,   A, B); }
binary_expression(R) ::= binary_expression(A) BITXOR  binary_expression(B). { R = new Ast(binary_expression, TOK_BITXOR,  A, B); }
binary_expression(R) ::= binary_expression(A) BITAND  binary_expression(B). { R = new Ast(binary_expression, TOK_BITAND,  A, B); }
binary_expression(R) ::= binary_expression(A) EQUALS  binary_expression(B). { R = new Ast(binary_expression, TOK_EQUALS,  A, B); }
binary_expression(R) ::= binary_expression(A) NEQUALS binary_expression(B). { R = new Ast(binary_expression, TOK_NEQUALS, A, B); }
binary_expression(R) ::= binary_expression(A) ST      binary_expression(B). { R = new Ast(binary_expression, TOK_ST,      A, B); }
binary_expression(R) ::= binary_expression(A) SE      binary_expression(B). { R = new Ast(binary_expression, TOK_SE,      A, B); }
binary_expression(R) ::= binary_expression(A) GT      binary_expression(B). { R = new Ast(binary_expression, TOK_GT,      A, B); }
binary_expression(R) ::= binary_expression(A) GE      binary_expression(B). { R = new Ast(binary_expression, TOK_GE,      A, B); }
binary_expression(R) ::= binary_expression(A) ADDOP   binary_expression(B). { R = new Ast(binary_expression, TOK_ADDOP,   A, B); }
binary_expression(R) ::= binary_expression(A) SUBOP   binary_expression(B). { R = new Ast(binary_expression, TOK_SUBOP,   A, B); }
binary_expression(R) ::= binary_expression(A) MULOP   binary_expression(B). { R = new Ast(binary_expression, TOK_MULOP,   A, B); }
binary_expression(R) ::= binary_expression(A) DIVOP   binary_expression(B). { R = new Ast(binary_expression, TOK_DIVOP,   A, B); }
binary_expression(R) ::= binary_expression(A) MODOP   binary_expression(B). { R = new Ast(binary_expression, TOK_MODOP,   A, B); }

// Unary expression
unary_expression(R) ::= postfix_expression(A) .      { R = A; } 
unary_expression(R) ::= ADDADD unary_expression(A) . { R = new Ast(prefix_expression, TOK_ADDADD, A); }
unary_expression(R) ::= SUBSUB unary_expression(A) . { R = new Ast(prefix_expression, TOK_SUBSUB, A); }

// Postfix expression
postfix_expression(R) ::= primary_expression(A) .                                 { R = A; }
postfix_expression(R) ::= postfix_expression(A) ADDADD .                          { R = new Ast(postfix_expression, TOK_ADDADD, A); }
postfix_expression(R) ::= postfix_expression(A) SUBSUB .                          { R = new Ast(postfix_expression, TOK_SUBSUB, A); }
postfix_expression(R) ::= postfix_expression(A) DOT id_expression(B).             { R = new Ast(member_expression, A, B); }
postfix_expression(R) ::= postfix_expression(A) LBRACKET expression(B) RBRACKET.  { R = new Ast(index_expression, A, B); }
postfix_expression(R) ::= postfix_expression(A) LPAREN argument_list(B) RPAREN .  { R = new Ast(function_call, A, B); }

// Primary expression
primary_expression(R) ::= literal(A) .                  { R = A; }
primary_expression(R) ::= id_expression(A) .            { R = A; }
primary_expression(R) ::= LPAREN expression(A) RPAREN . { R = A; }
primary_expression(R) ::= list_definition(A) .          { R = A; }

// Literals
literal(R) ::= INTEGER(A) . { R = new Ast(integer, String(A)); }
literal(R) ::= REAL(A) .    { R = new Ast(real,    String(A)); } 
literal(R) ::= STRING(A) .  { R = new Ast(string,  String(A)); }
literal(R) ::= TRUE .       { R = new Ast(boolean, true);  }
literal(R) ::= FALSE .      { R = new Ast(boolean, false); }
literal(R) ::= NULL .       { R = new Ast(null);  }

// Id expression
id_expression(R) ::= IDENTIFIER(A) . { R = new Ast(identifier, String(A)); }

// List definition
list_definition ::= list_start list_content list_end .
list_start   ::= LBRACKET .
list_end     ::= RBRACKET .
list_entry   ::= expression .
list_content ::= list_entry . 
list_content ::= list_content COMMA list_entry .

//////////////////////////////////////////////////////////////////////////
//
// Function calls
//

// An argument is any expression. If expression lists become part
// of the language, this will have to be changed to something else.
argument(R) ::= expression(A) . { R = A; }

// Argument list containing 1 or more arguments
arguments(R) ::= argument(A) . { R = A; }
arguments(R) ::= arguments(A) COMMA argument(B) . { R = new Ast(argument_list, A, B); }

// Argument list
argument_list(R) ::= . { R = 0; }
argument_list(R) ::= arguments(A) . { R = A; }

//////////////////////////////////////////////////////////////////////////
//
// Simple statements
//

// Expression statement
expression_statement(R) ::= SEMICOLON .               { R = 0; }
expression_statement(R) ::= expression(A) SEMICOLON . { R = new Ast(expression_statement, A); }

// Compound statement
compound_statement(R) ::= LBRACE statement_sequence_opt(A) RBRACE . { R = A; }

// Include statement
include_statement(R) ::= INCLUDE STRING(A) SEMICOLON . { R = new Ast(include_statement, String(A)); }

// Return statement - leaves return expression on the stack
return_statement(R) ::= RETURN expression(A) SEMICOLON . { R = new Ast(return_statement, A); }
return_statement(R) ::= RETURN SEMICOLON .               { R = new Ast(return_statement);    }

//////////////////////////////////////////////////////////////////////////
//
// Declarations
//

// Declaration starting point
declaration_statement(R) ::= declaration_sequence(A) SEMICOLON . { R = A; }
declaration_statement(R) ::= function_declaration(A) .           { R = A; }

// Declaration sequence
declaration_sequence(R) ::= VAR variable_declaration(A) . { R = A; }
declaration_sequence(R) ::= declaration_sequence(A) COMMA variable_declaration(B) . { R = new Ast(declaration_sequence, A, B); }

// Variable declaration
variable_declaration(R) ::= simple_declaration(A) . { R = A; }
variable_declaration(R) ::= init_declaration(A) .   { R = A; }

// Variable declaration
simple_declaration(R) ::= IDENTIFIER(A) . { R = new Ast(variable_declaration, String(A)); }

// Variable declaration with initialization
init_declaration(R) ::= IDENTIFIER(A) ASSIGN expression(B) . { R = new Ast(variable_declaration, String(A), B); }

// Function declaration
function_declaration(R) ::= FUNCTION IDENTIFIER(A) LPAREN parameter_list_opt(B) RPAREN function_body(C) . { R = new Ast(function_declaration, String(A), B, C); }

// Single parameter
parameter(R) ::= IDENTIFIER(A) . { R = new Ast(parameter, String(A)); }

// Parameters
parameter_list(R) ::= parameter(A) . { R = A; }
parameter_list(R) ::= parameter_list(A) COMMA parameter(B) . { R = new Ast(parameter_list, A, B); }

// Optional parameter list
parameter_list_opt(R) ::= . { R = 0; }
parameter_list_opt(R) ::= parameter_list(A) . { R = A; }

// Function body. Traditionally, this is a compound statement, but
// there's no actual need for that.
function_body(R) ::= statement(A) . { R = A; }

//////////////////////////////////////////////////////////////////////////
//
// For statement
//

// For statement - main structure
for_statement(R) ::= FOR LPAREN for_init_statement(A) expression(B) SEMICOLON expression_opt(C) RPAREN statement(D) . { R = new Ast(for_statement, A, B, C, D); }

// For init statement
for_init_statement(R) ::= expression_statement(A) .           { R = A; }
for_init_statement(R) ::= declaration_sequence(A) SEMICOLON . { R = A; }

//////////////////////////////////////////////////////////////////////////
//
// Foreach statement
//

foreach_statement(R) ::= FOR LPAREN id_expression(A) IN expression(B) RPAREN statement(C) . { R = new Ast(foreach_statement, A, B, C); }

//////////////////////////////////////////////////////////////////////////
//
// If statement
//

%left LOWER_THAN_ELSE.
%left ELSE.

// Main if statement
if_statement(R) ::= IF LPAREN expression(A) RPAREN statement(B) . [LOWER_THAN_ELSE] { R = new Ast(if_statement, A, B); } 
if_statement(R) ::= IF LPAREN expression(A) RPAREN statement(B) ELSE statement(C) . { R = new Ast(if_statement, A, B, C); }

//////////////////////////////////////////////////////////////////////////
//
// While statement
//

// Basic while
while_statement(R) ::= WHILE LPAREN expression(A) RPAREN statement(B) . { R = new Ast(while_statement, A,  B); }
